// Notice: Oreo builds are disabled by default until all problems are resolved!!!
//
// For Oreo compatibility we are forced to use supportLibVersion >= 26.1.0
// Unfortunately Android stopped supporting downloading libraries through the SDK Manager,
// @see https://developer.android.com/topic/libraries/support-library/setup.html
//
// You can enable oreo by adding following into root build.gradle:
//
// allprojects {
//   repositories {
//     maven { url 'https://maven.google.com' }
//   }
// }
//
// ext {
//   compileSdkVersion = 26
//   targetSdkVersion = 26
//   buildToolsVersion = "26.0.2"
//   supportLibVersion = "26.1.0"
//   googlePlayServicesVersion = "11.8.0"
//   oreoEXPERIMENTAL = "hellyes"
// }
//
//
// If you don't like this please ask react guys nicely to add google maven repo into
// their templates.

apply plugin: 'com.android.library'

apply from: '../common/VERSIONS.gradle'
def applicationId = getApplicationId()
def playServicesVersion = getPlayServicesVersion()
def supportLibVersion = getSupportLibVersion()
def hasGradle3Support = getGradle3Support();

def projectFlavor = "preoreo"
if (rootProject.hasProperty('oreoEXPERIMENTAL')) {
    projectFlavor = "oreo"
}

// https://hackernoon.com/android-how-to-add-gradle-dependencies-using-foreach-c4cbcc070458
def oreoDependencies = [
    [configuration: "implementation", dependency: libs.slf4j],
    [configuration: "implementation", dependency: libs.reactNative],
    [configuration: "testImplementation", dependency: testLibs.junit],
    [configuration: "testImplementation", dependency: testLibs.powermockMockito],
    [configuration: "testImplementation", dependency: testLibs.powermockjUnit],
    [configuration: "testImplementation", dependency: testLibs.powermockClassloading],
    [configuration: "testImplementation", dependency: testLibs.mockitoCore],
    [configuration: "testImplementation", dependency: testLibs.festAssertCore],
    [configuration: "testImplementation", dependency: testLibs.robolectric],
    [configuration: "androidTestImplementation", dependency: testLibs.testRunner],
    [configuration: "androidTestImplementation", dependency: testLibs.testRules],
]

def preoreoDependencies = [
    [configuration: "compile", dependency: libs.slf4j],
    [configuration: "compile", dependency: libs.reactNative],
    [configuration: "testCompile", dependency: testLibs.junit],
    [configuration: "testCompile", dependency: testLibs.powermockMockito],
    [configuration: "testCompile", dependency: testLibs.powermockjUnit],
    [configuration: "testCompile", dependency: testLibs.powermockClassloading],
    [configuration: "testCompile", dependency: testLibs.mockitoCore],
    [configuration: "testCompile", dependency: testLibs.festAssertCore],
    [configuration: "testCompile", dependency: testLibs.robolectric],
    [configuration: "androidTestCompile", dependency: testLibs.testRunner],
    [configuration: "androidTestCompile", dependency: testLibs.testRules],
]

repositories {
    // Google dependencies are now hosted at Maven
    // unfortunately this is ignored when installing as react-native plugin
    maven {
        url 'https://maven.google.com'
    }
    jcenter()
}

android {
    compileSdkVersion project.ext.getCompileSdkVersion()
    buildToolsVersion project.ext.getBuildToolsVersion()

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion project.ext.getMinSdkVersion()
        targetSdkVersion project.ext.getTargetSdkVersion()
        versionCode 1 // intentionally not updating version as we're not uploading to any java repository
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    if (hasGradle3Support) {
        implementation project(path: ':@mauron85_react-native-background-geolocation-common')

        oreoDependencies.each {
            add(it.configuration, it.dependency)
        }
    } else {
        releaseCompile project(path: ':@mauron85_react-native-background-geolocation-common', configuration: "${projectFlavor}Release")
        debugCompile project(path: ':@mauron85_react-native-background-geolocation-common', configuration: "${projectFlavor}Debug")

        preoreoDependencies.each {
            add(it.configuration, it.dependency)
        }
    }
}
